-- Programs ------------------------------------------------
entrypoints Program ;
Prgm.   Program  ::= [TopDef] ;
FnDef.	   TopDef   ::= Type Ident "(" [Arg] ")" Block ;
separator  nonempty TopDef "" ;
Argument.  Arg      ::= Type Ident ;
separator  Arg "," ;
ClsDef.    TopDef   ::= "class" Ident "{" [ClsBowel] "}" ;
ClsExtDef. TopDef   ::= "class" Ident "extends" Ident "{" [ClsBowel] "}" ;

-- MethodBowels --------------------------------------------
MemberDef. ClsBowel ::= Type [Item] ";" ;
MethodDef. ClsBowel ::= Type Ident "(" [Arg] ")" Block ;
separator  ClsBowel  "" ;

-- Statements ----------------------------------------------
Blk.       Block ::= "{" [Stmt] "}" ;
separator  Stmt "" ;
Empty.     Stmt ::= ";" ;
BStmt.     Stmt ::= Block ;
Decl.      Stmt ::= Type [Item] ";" ;
NoInit.    Item ::= Ident ;
Init.      Item ::= Ident "=" Expr ;
separator  nonempty Item "," ;
Ass.       Stmt ::= Expr "=" Expr  ";" ;
--AssMember. Stmt ::= Ident "." Ident "=" Expr  ";" ;
Incr.      Stmt ::= Ident "++"  ";" ;
Decr.      Stmt ::= Ident "--"  ";" ;
Ret.       Stmt ::= "return" Expr ";" ;
VRet.      Stmt ::= "return" ";" ;
Cond.      Stmt ::= "if" "(" Expr ")" Stmt ;
CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;
While.     Stmt ::= "while" "(" Expr ")" Stmt ;
SExp.      Stmt ::= Expr  ";" ;


-- Types ---------------------------------------------------
-- Int.       Type ::= "int" ;
-- Str.       Type ::= "string" ;
-- Bool.      Type ::= "boolean" ;
IdentType. Type ::= Ident ;
Void.      Type ::= "void" ;
internal   Fun. Type ::= Type "(" [Type] ")" ;
separator  Type "," ;

-- Expressions ---------------------------------------------
ECast.     Expr6 ::= "(" Ident ")null" ;
EVar.      Expr6 ::= Ident;
EMember.   Expr5 ::= Expr5 "." Ident; --daje konflikt
EConstr.   Expr6 ::= "new" Ident ;
ELitInt.   Expr6 ::= Integer ;
ELitTrue.  Expr6 ::= "true" ;
ELitFalse. Expr6 ::= "false" ;
EMethApp.  Expr5 ::= Expr5 "." Ident "(" [Expr] ")" ; --daje konflikt
EApp.      Expr6 ::= Ident "(" [Expr] ")" ;
EString.   Expr6 ::= String ;
Neg.       Expr5 ::= "-" Expr6 ;
Not.       Expr5 ::= "!" Expr6 ;
EMul.      Expr4 ::= Expr4 MulOp Expr5 ;
EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;
ERel.      Expr2 ::= Expr2 RelOp Expr3 ;
EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;
EOr.       Expr  ::= Expr1 "||" Expr ;
coercions  Expr 6 ;
separator  Expr "," ;

-- Operators -----------------------------------------------
Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

-- Comments ------------------------------------------------
comment    "#" ;
comment    "//" ;
comment    "/*" "*/" ;
